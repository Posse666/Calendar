plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
}
android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    signingConfigs {
        debug {
            Properties properties = new Properties()
            properties.load(project.rootProject.file("signingKey.properties").newDataInputStream())

            storeFile file(properties.getProperty("path_to_store_file", ""))
            storePassword properties.getProperty("store_password", "")
            keyAlias properties.getProperty("key_alias", "")
            keyPassword properties.getProperty("key_password", "")
        }
    }

    defaultConfig {
        applicationId "com.posse.kotlin1.calendar"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            versionNameSuffix "-debug"
            buildConfigField "String", "TYPE", '"DEBUG"'
        }
    }

    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("fcm.properties").newDataInputStream())
        def serverKey  = properties.getProperty("server_key", "")
        it.buildConfigField "String", "LEGACY_SERVER_KEY", serverKey
    }

    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    //firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.3')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-messaging-directboot'

    //gms
    implementation 'com.google.android.gms:play-services-auth:20.0.1'

    //picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    //view
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.fragment:fragment-ktx:1.4.0"

    //viewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'

    //calendar
    implementation 'com.github.kizitonwose:CalendarView:1.0.4'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'

    // Dagger
    implementation 'com.google.dagger:dagger-android:2.40.1'
    implementation 'com.google.dagger:dagger-android-support:2.40.1'
    kapt 'com.google.dagger:dagger-android-processor:2.38'
    kapt 'com.google.dagger:dagger-compiler:2.40.1'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}