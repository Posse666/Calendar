plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
//    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.posse.kotlin1.calendar"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 7
        versionName "2.0"
        resConfigs 'ru'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.3.2"
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file("signingKey.properties").newDataInputStream())

            storeFile file(properties.getProperty("path_to_store_file", ""))
            storePassword properties.getProperty("store_password", "")
            keyAlias properties.getProperty("key_alias", "")
            keyPassword properties.getProperty("key_password", "")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            kotlinOptions {
                jvmTarget = '1.8'
                freeCompilerArgs += ['-Xno-call-assertions',
                                     '-Xno-receiver-assertions',
                                     '-Xno-param-assertions']
            }
        }

        debug {
            signingConfig signingConfigs.release
            versionNameSuffix ".debug"
            buildConfigField "String", "TYPE", '"DEBUG"'
        }
    }

    packagingOptions {
        resources.excludes += "DebugProbesKt.bin"
    }

    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = ['-Xjvm-default=enable']
    }
    namespace 'com.posse.kotlin1.calendar'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
}

dependencies {

    // firebase
    implementation platform('com.google.firebase:firebase-bom:31.0.2')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-messaging-directboot'
    implementation 'com.google.firebase:firebase-common-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'

    // gms
    implementation 'com.google.android.gms:play-services-auth:20.3.0'

    // coil
    implementation 'io.coil-kt:coil:2.2.2'

    // splash_screen
    implementation 'androidx.core:core-splashscreen:1.0.0'

    // ui
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'

    // gson
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // dagger - Hilt
    implementation 'com.google.dagger:hilt-android:2.44'
    kapt 'com.google.dagger:hilt-android-compiler:2.44'
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.4'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.0-alpha03"

    // compose
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.0-alpha03'
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.27.0'

    //compose tooling
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // navigation
    implementation 'com.google.accompanist:accompanist-navigation-animation:0.27.0'
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // memory_leaks
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
}

apply plugin: 'com.google.gms.google-services'